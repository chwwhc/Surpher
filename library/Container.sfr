fixed namespace Container{

    fixed class ContainerSignature {
        sig class copy();
        // sig sort();
        // sig getIdx();
        // sig popBack();
        // sig addBack();
        // sig popFront();
       // sig popIdx();

        sig __sizeOf__();
        sig __toString__();
    }

    fixed class Vector < ContainerSignature {
        init(arr){
            this.arr = arr;
            this.size = sizeOf(arr);
        }

        __sizeOf__(){
            return this.size;
        }

        __toString__(){
            return String.toString(this.arr);
        }

        class copy(other){
            var new_arr = [alloc: sizeOf(other)];
            for(var i = 0; i < sizeOf(other); i = i + 1){
                @i->new_arr = @i->other.arr;
            }
            return Container.Vector(new_arr);
        }

        filter(pred){
            var ret = Container.Vector([]);
            for(var i = 0; i < this.size; i = i + 1){
                var curr = this.getIdx(i);
                if(pred(curr))
                    ret.addBack(curr);
            }

            return ret;
        }

        map(f){
            for(var i = this.size - 1; i >= 0; i = i - 1)
                this.setIdx(i, f(this.getIdx(i)));
        }

        foldRight(f, unit){
            for(var i = this.size - 1; i >= 0; i = i - 1)
                unit = f(this.getIdx(i), unit);
            return unit;
        }

        foldLeft(f, unit){
            for(var i = 0; i < this.size; i = i + 1)
                unit = f(unit, this.getIdx(i));
            return unit;
        }

        sort(cmp){
            fun merge(arr, left, right){
                var i = 0, j = 0, k = 0;
                var left_len = sizeOf(left), right_len = sizeOf(right);

                while(i < left_len and j < right_len){
                    if(cmp(@i->left, @j->right)){
                        @k->arr = @i->left;
                        i = i + 1;
                    }else{
                        @k->arr = @j->right;
                        j = j + 1;
                    }
                    k = k + 1;
                }

                while(i < left_len){
                    @k->arr = @i->left;
                    i = i + 1;
                    k = k + 1;
                }

                while(j < right_len){
                    @k->arr = @j->right;
                    j = j + 1;
                    k = k + 1;
                }
            }

            fun mergeSort(arr){
                if(sizeOf(arr) < 2) return;

                var len = sizeOf(arr);
                var mid = Math.floor(len / 2);
                var left = [alloc: mid];
                var right = [alloc: len - mid];

                for(var i = 0; i < mid; i = i + 1){
                    @i->left = @i->arr;
                }

                for(var i = mid; i < len; i = i + 1)
                    @(i - mid)->right = @i->arr;

                mergeSort(left);
                mergeSort(right);

                merge(arr, left, right);
            }

            mergeSort(this.arr);
        }

        getIdx(idx){
            if(idx < this.size and idx >= 0){
                return @idx->this.arr;
            }else{
                halt "invalid index.";
            }
        }

        setIdx(idx, value){
            if(idx >= 0 and idx < this.size){
                @idx->this.arr = value;
            }else{
                halt "invalid index.";
            }
        }

        popBack(){
            if(this.size > 0){
                var new_arr = [alloc: this.size - 1];
                for(var i = 0; i < this.size - 1; i = i + 1){
                    @i->new_arr = @i->this.arr;
                }
                this.arr = new_arr;
                this.size = this.size - 1;
            }else{
                halt "cannot pop from an empty vector.";
            }
        }

        addBack(new_elem){
            var new_arr = [alloc: this.size + 1];
            for(var i = 0; i < this.size; i = i + 1){
                @i->new_arr = @i->this.arr;
            }
            @(this.size)->new_arr = new_elem;
            this.arr = new_arr;
            this.size = this.size + 1;
        }

        popFront(){
            if(this.size > 0){
                var new_arr = [alloc: this.size - 1];
                for(var i = 1; i < this.size; i = i + 1){
                    @(i - 1)->new_arr = @i->this.arr;
                }
                this.arr = new_arr;
                this.size = this.size - 1;
            }else{
                halt "cannot pop from an empty vector.";
            }
        }
    }
}